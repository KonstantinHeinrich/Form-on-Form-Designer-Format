
Функция СтрокаJSON(Структура) Экспорт
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, Структура);
	Возврат Запись.Закрыть();
	
КонецФункции

Функция ЗначениеJSON(Строка) Экспорт
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Строка);
	Структура = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();
	Возврат Структура;
	
КонецФункции

Функция СтрокаXML(Значение) Экспорт
	
	Возврат СериализаторXDTO.XMLСтрока(Значение);
	
КонецФункции

Функция ЗначениеXML(Тип, Строка) Экспорт
	
	Возврат СериализаторXDTO.XMLЗначение(Тип, Строка)
	
КонецФункции

Процедура НастроитьИнтерфейс(СкрытьНачальнуюСтраницу) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КлючОбъекта         = "Общее/НастройкиНачальнойСтраницы";
	КлючОбъектаХранения = "Общее/НастройкиНачальнойСтраницыПередОчисткой";
	СохраненныеНастройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъектаХранения, "");
	
	Если СкрытьНачальнуюСтраницу Тогда
		
		Если ТипЗнч(СохраненныеНастройки) <> Тип("ХранилищеЗначения") Тогда
			ТекущиеНастройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта);
			СохраняемыеНастройки = Новый ХранилищеЗначения(ТекущиеНастройки);
			ХранилищеСистемныхНастроек.Сохранить(КлючОбъектаХранения, "", СохраняемыеНастройки);
		КонецЕсли;
		
		ТекущиеНастройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта);
		  				
		СоставФорм = Новый СоставФормНачальнойСтраницы;
		Настройки = Новый НастройкиНачальнойСтраницы;
		Настройки.УстановитьСоставФорм(СоставФорм);
		ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, "", Настройки);
		
	Иначе
		
		Если ТипЗнч(СохраненныеНастройки) = Тип("ХранилищеЗначения") Тогда
			СохраненныеНастройки = СохраненныеНастройки.Получить();
			Если СохраненныеНастройки = Неопределено Тогда
				ХранилищеСистемныхНастроек.Удалить(КлючОбъекта, Неопределено,
					ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
			Иначе
				ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, "", СохраненныеНастройки);
			КонецЕсли;
			ХранилищеСистемныхНастроек.Удалить(КлючОбъектаХранения, Неопределено,
				ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры