
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ФНФ_Клиент.ОткрытьФормуВоВложенной(Элементы.ПодборНоменклатуры, "Справочник.Номенклатура.Форма.ФормаПодбора");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборНоменклатурыПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Сообщение = ФНФ_Клиент.СообщениеОтВложенной(Элемент, ДанныеСобытия);
	Если ПустаяСтрока(Сообщение) Тогда
		Возврат;
	КонецЕсли; 
	
	Части = СтрРазделить(Сообщение, ":", Истина);
	УнИд = Части[0];
	Колво = Части[1];
	
	Товар = ПолучитьНоменклатуру(УнИд);
	НайдСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", Товар));
	
	Если ПустаяСтрока(Колво) Тогда //Получение кол-ва
		
		ТекКолво = ?(НайдСтроки.Количество() = 0, 1, НайдСтроки[0].Количество);
		ФНФ_Клиент.ВыполнитьОповещениеВложенной(Элементы.ПодборНоменклатуры, "Форма", "ВводКоличества", ТекКолво);
				
	Иначе //Установка кол-ва
		
		Если НайдСтроки.Количество() = 0 Тогда
			ТекСтрока = Объект.Товары.Добавить();
			ТекСтрока.Номенклатура = Товар;
		Иначе
			ТекСтрока = НайдСтроки[0];
		КонецЕсли;
		ТекСтрока.Количество = Число(Колво);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуру(СтрокаУнИд)
	
	УнИд = Новый УникальныйИдентификатор(СтрокаУнИд);
	Возврат Справочники.Номенклатура.ПолучитьСсылку(УнИд);
		
КонецФункции